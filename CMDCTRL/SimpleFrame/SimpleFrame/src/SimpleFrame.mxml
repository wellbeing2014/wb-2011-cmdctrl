<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" 
	xmlns:col="common.*" xmlns:local="*" xmlns:ns1="flexlib.containers.*" preinitialize="pinit()"
    creationComplete="init()">
   
    <mx:Script>
    	<![CDATA[
    		import mx.collections.ArrayCollection;
    		import mx.events.MenuEvent;
    		import mx.rpc.events.ResultEvent;
    	
    		/*** 解决Manager类Bug,在使用Module时此段代码必须要加，要不然在Module反复载入时会出错****/
			import mx.managers.DragManager;
			import mx.managers.PopUpManager;
			import mx.managers.HistoryManager;
			private var dragManager:DragManager;
			private var popupManager:PopUpManager;
			private var historyManager:HistoryManager;
			
			/****结束 解决Manager类Bug************/
			import mx.events.ItemClickEvent;
			import com.wisoft.interfaces.ILoadUserInfo;
			import com.wisoft.interfaces.ILoadSevicesURL;
			import com.wisoft.vo.Department;
			import com.wisoft.vo.Person;
			import com.wisoft.vo.UserInfo;
			import com.wisoft.framework.oms.OmsSelectPerson2;
			import com.wisoft.util.CommonFunctions;
			import mx.events.ModuleEvent;
			import com.wisoft.events.WindowCloseEvent;
			import com.wisoft.vo.SysConst; 
			import com.wisoft.framework.oms.CommonSelectDep;
			import com.wisoft.events.SysEvent;	
			import com.wisoft.util.WebServiceHelper;
			import com.wisoft.framework.oms.CommonSelectSecretary;
			import mx.events.CollectionEvent;
			import mx.modules.ModuleLoader;
			
			private var personid:String="";
			private var moduleurl:String="";
			private var _ZONECODE:String="65310100";
			private var _SERVICEURL:String="http://58.214.246.41:8021/NewFrame/services";
			[Bindable]private var _personname:String = "未指定";
			//当前人员信息
			private var _USERINFO:UserInfo = new UserInfo();
			//常用人员信息列表
			[Bindable]private var PersonList:ArrayCollection = new ArrayCollection();
			//当前服务
			[Bindable]private var ServerObject:Object = new Object();
			
			/**对象名：ServerList
			 * 包含对象:
			 * 1、serverList 每个server属性   name\address\zone\orderno  users
			 ****/
			private var shareData:SharedObject;
			private var departmentWithParent:Object;
			
			//---------------------------------------
			[Bindable] private var startMenuBarList:ArrayCollection; //菜单栏
			[Bindable] private var appList:ArrayCollection;
			
			private function pinit():void{
				StyleManager.loadStyleDeclarations('css/style01/style01.swf');
				//设置安全沙箱
				Security.allowDomain("*");
			}
			private function init():void{
				//首先给全局用户初始化
				SysConst.curUser = new UserInfo();
				//读取本地存储的服务
				shareData=SharedObject.getLocal("ServerObject");
				ServerObject=shareData.data.ServerObject as Object;
				
				//给本地数据进行初始化
				if(ServerObject!=null)
				{
					this.linkbtn.label = ServerObject.name;//获取服务名称赋值给当前linkbtn;
					PersonList = ServerObject.users;//获取人员列表；
				}
				else
					CommonFunctions.showAlert("提示","请设置服务及登录人员！");
				
				if(PersonList!=null&&PersonList.length>0)
				{
					//默认取第一个人员为当前人员
					_USERINFO = PersonList[0] as UserInfo;
					//给人员设置PopupBtn的标签为机构+姓名
					_personname=_USERINFO.ou.depname+"|"+_USERINFO.person.realname;
					personid=_USERINFO.person.id;
					SysConst.curUser = _USERINFO;
					sel_personsyle();
				}
//				moduleldr.url="zdm\\LoadZd.swf";
			}
			
			private var systemServiceTempList:ArrayCollection;
			private var serviceLoaderList:ArrayCollection;
			/**
			 * 创建用于顺序加载模块的列表
			 * */
			private function createSystemServiceTempList(systemServiceList:ArrayCollection):void {
				systemServiceTempList = new ArrayCollection();
				systemServiceTempList.addEventListener(CollectionEvent.COLLECTION_CHANGE, function(e1:CollectionEvent):void {
						if(e1.location > 0) {
							var prvObj:Object = systemServiceTempList.getItemAt(e1.location - 1);
							var curObj:Object = systemServiceTempList.getItemAt(e1.location);
							var prvLoader:ModuleLoader = prvObj.loader as ModuleLoader;
							var curLoader:ModuleLoader = curObj.loader as ModuleLoader;
							prvLoader.addEventListener(ModuleEvent.READY, function(e2:ModuleEvent):void {
								// 当前一个module加载完后加载下一个
									curLoader.loadModule(curObj.url as String);
								});
						}
					});
				serviceLoaderList = new ArrayCollection();
				for each(var systemService:Object in systemServiceList) {
					var obj:Object = createSystemServiceTempListItem(systemService.accessaddress);
					vs.addChild(obj.loader);
					serviceLoaderList.addItem(obj.loader);
					systemServiceTempList.addItem(obj);
				}
			}
			
			/**
			 * 创建用于顺序加载模块的列表对象
			 * */
			private function createSystemServiceTempListItem(url:String):Object {
				var obj:Object = new Object();
				var loader:ModuleLoader = new ModuleLoader();
				obj.loader = loader;
				obj.url = url;
				return obj;
			}
			
			private function getSystemServices():void {
				ws_ams.findSysServ.send();
			}

			private function resultFindSysServ(event:ResultEvent):void {
				if(event.result != null) {
					if(event.result.isSuccued) {
						var systemServiceList:ArrayCollection = WebServiceHelper.convert2List2(event.result.amsApplication);
						vs.removeAllChildren();
						if(systemServiceList.length > 0) {
							createSystemServiceTempList(systemServiceList);
							var firstObj:Object = systemServiceTempList.getItemAt(0);
							(firstObj.loader as ModuleLoader).loadModule(firstObj.url);
						} else {
							init();
						}
					} else {
						CommonFunctions.showInfo("提示", event.result.message);
					}
				}
			}
			
			private function selectPersonByZoneid():void
			{
				var selectPage:OmsSelectPerson2=OmsSelectPerson2(PopUpManager.createPopUp(this, OmsSelectPerson2, true));
				selectPage.servicesurl=_SERVICEURL;
				selectPage.deptype=-1;
				selectPage.iscanuse=-1;
				selectPage.ismultiselect=true;
				selectPage.zonecode=this._ZONECODE;
				selectPage.addEventListener(WindowCloseEvent.WINDOW_CLOSE, selectWindowClose);
				CommonFunctions.centerTitleWindow(selectPage);
			}

			/*选人之后，保存*/
			private function selectWindowClose(event:WindowCloseEvent):void
			{
				/*返回选中的人员IS集合*/
				var resultList:ArrayCollection=event.ReturnValue as ArrayCollection;
				for	each(var a:Object in resultList )
				{
					ws_oms.findOmsInfoByPersonid.send(a.id);
				}
			}
			
			private function resultFindOmsInfoByPersonid(event:ResultEvent):void {
				if(event.result) {
					if(event.result.isSuccued) {
						var userinfo:UserInfo = new UserInfo;
						userinfo.authlist = event.result.authlist;
						var dt:Department = Department.convert(event.result.department);
						userinfo.department = dt;
						userinfo.deplist = event.result.deplist;
						userinfo.isadmin = event.result.isadmin;
						userinfo.isdepadmin = event.result.isdepadmin;
						userinfo.ismultdep = event.result.ismultdep;
						userinfo.iszoneadmin = event.result.iszoneadmin;
						userinfo.ou = Department.convert(event.result.ou);
						userinfo.person = Person.convert(event.result.person);
						userinfo.rolegrouplist = event.result.rolegrouplist;
						userinfo.teamlist = event.result.teamlist;
						
						//只添加列表中没有的
						if(PersonList.length>0)
						{	
							var ishave:Boolean=false;
							for each(var a:Object in PersonList )
							{
								if(a.person.id == userinfo.person.id)
								{
									ishave=true;
									break;
								}
							}
							if(!ishave)
							{
								PersonList.addItem(userinfo);
							}
						}
						else
							PersonList.addItem(userinfo);
							
						ServerObject.users = PersonList;
						shareData.data.ServerObject = ServerObject;
						shareData.flush();
					}
				} else {
					CommonFunctions.showError("错误", event.result.message);
				}
			}
			
			private function unsel_person_done():void
			{
				var userinfo:UserInfo=UserInfo.convert(dgofpro.selectedItem) ;
				for (var a:int=0; a<PersonList.length;a++ )
				{
					if(PersonList[a].person.id == userinfo.person.id)
					{
						PersonList.removeItemAt(a);
						break;
					}
				}
				ServerObject.users = PersonList;
				shareData.data.ServerObject = ServerObject;
				shareData.flush();
			}
			
			private function sel_person_done():void
			{
				_USERINFO=dgofpro.selectedItem as UserInfo;
				
				if(_USERINFO==null)
				{
					CommonFunctions.showAlert("提示","没有人员被选中！");
					return;
				}
				personid=_USERINFO.person.id;
				
				
				//moduleldr.url="zdm\\LoadZd.swf";
				trace("用户信息加载成功");

				//存储本地数据
				shareData.data.curuser =_USERINFO;
				shareData.flush();
				SysConst.curUser = _USERINFO;
				_personname = _USERINFO.ou.depname+"|"+_USERINFO.person.realname;
				proid.close();
				sel_personsyle();
			}
			
			private function sel_personsyle():void
			{
				if(_USERINFO.ismultdep) {
					trace("用户包含多机构，选择登录机构");
					var selectdep:CommonSelectDep = CommonSelectDep(PopUpManager.createPopUp(this, CommonSelectDep, true));
					if(_USERINFO.deplist) {
						selectdep.deplist = _USERINFO.deplist;
						selectdep.person = _USERINFO.person;
					}
					selectdep.addEventListener(WindowCloseEvent.WINDOW_CLOSE, selectDepartmentWindowClosed);
					CommonFunctions.centerTitleWindow(selectdep);
					return;
				}
				
				if(SysConst.curUser.person.id == personid) {
					trace("以自己身份登录");
					if(SysConst.curUser.isadmin) {
						trace("为超级管理员，查找所有应用和应用分组");
						ws_ams.findAmsInfoBySuperAdmin.send();
					} else {
						trace("为普通用户，根据personid和选择的机构id查找应用和应用分组");
						ws_ams.findAmsInfoByPersonidAndOu.send(SysConst.curUser.person.id, SysConst.curUser.ou.depid);
					}
				} else {
					trace("以领导身份登陆，查找领导分配的应用和应用分组");
					ws_secretary.findAmsInfoByLeaderidAndSecretaryid(SysConst.curUser.person.id, personid);
				}
			}
			
			
			private function selectDepartmentWindowClosed(event:WindowCloseEvent):void {
				if(event.IsConfirm) {
					if(event.RelatedObject) {
						_USERINFO.department = event.RelatedObject[0].department as Department;
						_USERINFO.ou = event.RelatedObject[1] as Department;
						_USERINFO.rolegrouplist = event.RelatedObject[2] as ArrayCollection;
						departmentWithParent = event.RelatedObject[0];
						trace("机构选择成功");
						if(SysConst.curUser.person.id == personid) {
							trace("以自己身份登录");
							if(SysConst.curUser.isadmin) {
								trace("为超级管理员，查找所有应用和应用分组");
								ws_ams.findAmsInfoBySuperAdmin.send();
							} else {
								trace("为普通用户，根据personid和选择的机构id查找应用和应用分组");
								ws_ams.findAmsInfoByPersonidAndOu.send(SysConst.curUser.person.id, SysConst.curUser.ou.depid);
							}
						} else {
							trace("以领导身份登陆，查找领导分配的应用和应用分组");
							ws_secretary.findAmsInfoByLeaderidAndSecretaryid(SysConst.curUser.person.id, personid);
						}
					}
				}
			}
			
			private function ChangeServer():void
			{ 
				var serverCfg:ConfigServer = ConfigServer(PopUpManager.createPopUp(this,ConfigServer,true));
				serverCfg.parentfunc=callbackfunction;
				PopUpManager.centerPopUp(serverCfg);
			}
			
			private function callbackfunction(server:Object):void
			{
				this.linkbtn.label=server.name;
				if(this.ServerObject.name!=server.name)
				{
					this.ServerObject.name=server.name;
					this.ServerObject.address=server.address;
					this.ServerObject.zone=server.zone;
					this.ServerObject.orderno=server.orderno;
					this.ServerObject.users=null;	
				}
				else
				{
					
				}
						
			}
			
			
			/**
			 * 初始化开始菜单

			 */
			 
			 private function initStartMenu():void {
			
				appList = SysConst.curUser.applist;
				var appGroupList1:ArrayCollection = SysConst.curUser.menulgroupist;
				var arr:ArrayCollection = new ArrayCollection();
				var appGroupList:ArrayCollection = new ArrayCollection();

				
				//获取所有菜单组下的所有菜单
				for each(var group:Object in appGroupList1){
					var children:ArrayCollection = new ArrayCollection();
					for each(var subGroup:Object in appGroupList1){
						if(subGroup.parentid == group.id){
							subGroup.itemType = "group";
							children.addItem(subGroup);
						}
					}
					for each(var app:Object in appList){
						if(app.groupid == group.id){
							app.itemType = "app";
							children.addItem(app);
						}
					}
					
					if (children.length > 0) {
						group.children = children;
					}
					
					if(group.parentid == "root"){
						group.itemType = "group";
						arr.addItem(group);
					}
				}
				startMenuBarList = arr;
				startMenuBar.addEventListener(StartMenu2.APPLICATION_CLICKED, applicationClickedHandler);
			}
			
			private function applicationClickedHandler(event:SysEvent):void {
				loadMenu(event.relatedObject);
			}
			
			
			
			/**
			 * 添加Tab页

			 */
			public function loadMenu(menuitem:Object):void {
				var tb:TabModule = new TabModule();
				tb.serviceUrl = _SERVICEURL;
				tb.userinfo = SysConst.curUser;
				tb.moduleUrl = menuitem.accessaddress;
				tb.tabLabel = menuitem.name;
				tb.appcode = menuitem.appcode;
				tb.id = menuitem.id; //将标签页ID 设置为UID
				//判断用户是否设置页面多开，若唯一，则给与提示
				if(menuitem.isunique && menuitem.isunique == 1) {
					var children:Array = workzone.getChildren();
					var flag:Boolean = true;
					for(var i:Number = 0; i < children.length; i++) {
						var obj:Object = children[i];
						if(obj.id && obj.id == tb.id) {
							//查找到该应用已经打开则弹出提示对话框
							flag = false;
							workzone.selectedIndex = i;
						}
					}
					if(true == flag) {
						workzone.addChild(tb);
						flash.utils.setTimeout(function():void {
								workzone.selectedIndex = workzone.getChildIndex(tb);
							}, 100, null);
					} else {
						return;
					}
				} else {
					workzone.addChild(tb);
					flash.utils.setTimeout(function():void {
							workzone.selectedIndex = workzone.getChildIndex(tb);
						}, 100, null);
				}
			}
			
			
			private function resultFindAmsInfo(event:ResultEvent):void {
				if(event.result) {
					if(event.result.isSuccued) {
						trace("更换登录界面为加载界面");
						//showProgressBar();
						
						trace("应用和应用分组加载成功");
						SysConst.curUser.applist = event.result.applist;
						SysConst.curUser.menulgroupist = event.result.menulgroupist;
						
						initStartMenu();
					} else {
						CommonFunctions.showError("错误", event.result.message);
					}
				}
			}
			
			private function resultFindLeadersBySecretaryid(event:ResultEvent):void {
				if(event.result) {
					if(event.result.isSuccued) {
						var leaders:ArrayCollection = WebServiceHelper.convert2List2(event.result.omsPerson);
						if(leaders.length == 0) {
							trace("不是任何人的秘书");
							ws_oms.findOmsInfoByPersonid.send(personid);
						} else {
							trace("包含领导信息，选择登录身份")
							var selectSecretary:CommonSelectSecretary = CommonSelectSecretary(PopUpManager.createPopUp(this, CommonSelectSecretary, true));
							selectSecretary.personid = personid;
							selectSecretary.leaderlist = leaders;
							selectSecretary.addEventListener(WindowCloseEvent.WINDOW_CLOSE, selectSecretaryWindowClosed);
							CommonFunctions.centerTitleWindow(selectSecretary);
						}
					} else {
						CommonFunctions.showError("错误", event.result.message);
					}
				}
			}
			
			private function selectSecretaryWindowClosed(event:WindowCloseEvent):void {
				if(event.IsConfirm) {
					if(event.RelatedObject) {
						trace("身份选择成功，以personid为" + event.RelatedObject + "的用户的身份登录系统");
						ws_oms.findOmsInfoByPersonid.send(event.RelatedObject);
					}
				}
			}
    	]]>
    </mx:Script>  
    
	<mx:VBox left="0" right="0" bottom="0" top="0">
		<mx:HBox width="100%" height="70">
			<mx:VBox width="100%" height="100%" verticalGap="0">
				<mx:HBox width="100%" height="40" backgroundColor="#059BFB" horizontalAlign="left" verticalAlign="middle">
					<mx:Label text="Wisoft自动化测试平台 for RiaTest" fontSize="27" fontWeight="bold" color="#FFFFFF" fontStyle="italic"/>
					<mx:Spacer width="100%"/>
					<mx:LinkButton id="linkbtn" label="未指定服务" click="ChangeServer()"/>
				</mx:HBox>
				<mx:HBox width="100%" height="100%" horizontalAlign="left" verticalAlign="middle">
					<local:StartMenuBar id="startMenuBar" width="100%" height="28" Menulist="{startMenuBarList}" />
					<mx:Spacer width="100%"/>
					<mx:PopUpButton id="proid" label="{_personname}" openAlways="true" width="160" textAlign="left" toolTip="{proid.label}">
						<mx:popUp>
							<mx:Canvas  width="160" height="200"> 
							<mx:VBox>
							<mx:DataGrid id="dgofpro" dataProvider="{PersonList}" height="100%" textAlign="left" width="100%"
											   horizontalScrollPolicy="auto" borderStyle="none"   >
							<mx:columns> 
								<col:ObjectNestedColumn headerText="部门" dataField="ou.depname" width="80" sortable="false" />
								<col:ObjectNestedColumn headerText="姓名"  dataField="person.realname" width="80" sortable="false"  >
								</col:ObjectNestedColumn>
							</mx:columns> 
							</mx:DataGrid>
							<mx:ApplicationControlBar width="100%" >
								<mx:Button width="40" label="添加" click="selectPersonByZoneid()" ></mx:Button>
								<mx:Button width="40" label="删除" click="unsel_person_done()" ></mx:Button>
								<mx:Spacer width="100%"/>
								<mx:Button width="40" label="确定" click="sel_person_done()" ></mx:Button>
							</mx:ApplicationControlBar>
							</mx:VBox>	
						</mx:Canvas>
						</mx:popUp>
					</mx:PopUpButton>
				</mx:HBox>
			</mx:VBox>
		</mx:HBox>
		<ns1:SuperTabNavigator id="workzone" width="100%" height="100%" paddingLeft="0">
			
		</ns1:SuperTabNavigator>
		<mx:ViewStack id="vs" width="0" height="0" />
	</mx:VBox>
	<mx:WebService id="ws_oms" wsdl="{_SERVICEURL}/OmsManager?wsdl" fault="CommonFunctions.faultmethod(event)">
		<mx:operation name="findOmsInfoByPersonid" result="resultFindOmsInfoByPersonid(event)" />
	</mx:WebService>
	<mx:WebService id="ws_ams" wsdl="{_SERVICEURL}/ApplicationManager?wsdl" fault="CommonFunctions.faultmethod(event)">
		<mx:operation name="findAmsInfoBySuperAdmin" result="resultFindAmsInfo(event)" />
		<mx:operation name="findAmsInfoByPersonid" result="resultFindAmsInfo(event)" />
		<mx:operation name="findAmsInfoByPersonidAndOu" result="resultFindAmsInfo(event)" />
		<mx:operation name="findSysServ" result="resultFindSysServ(event)" />
	</mx:WebService>
	<mx:WebService id="ws_secretary" wsdl="{_SERVICEURL}/SecretaryManager?wsdl">
		<mx:operation name="findAmsInfoByLeaderidAndSecretaryid" result="resultFindAmsInfo(event)" />
		<mx:operation name="findLeadersBySecretaryid" result="resultFindLeadersBySecretaryid(event)" />
	</mx:WebService>
	
</mx:Application>
