<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" 
	xmlns:col="common.*" preinitialize="pinit()"
    creationComplete="init()">
	
	<mx:XMLList id="myXMLList">  
	    <item name="事项维护" moduleurl ="">  
	        <item  name ="报审事项" moduleurl ="aims\\AuditManager.swf" />  
	        <item  name ="事项维护" moduleurl ="aims\\ItemManager.swf" />  
	    </item>  
	    <item  name ="许可平台" moduleurl ="" >  
	        <item name ="办件办理" moduleurl ="maea\\bjbl.swf"/>  
	        <item name ="办件查询" moduleurl ="maea\\bjcx.swf" />  
	    </item>  
	    
	    <item  name ="后台框架" moduleurl ="" >  
	        <item name ="任务调度管理" moduleurl ="jm/JobManageIndex.swf"/>  
	        <item name ="组织机构管理" moduleurl ="oms/OmsIndex_wx.swf" />  
	    </item>  
    </mx:XMLList>
   
    <mx:Script>
    	<![CDATA[
    		import mx.collections.ArrayCollection;
    		import mx.events.MenuEvent;
    		import mx.rpc.events.ResultEvent;
    	
    		/*** 解决Manager类Bug,在使用Module时此段代码必须要加，要不然在Module反复载入时会出错****/
			import mx.managers.DragManager;
			import mx.managers.PopUpManager;
			import mx.managers.HistoryManager;
			private var dragManager:DragManager;
			private var popupManager:PopUpManager;
			private var historyManager:HistoryManager;
			
			/****结束 解决Manager类Bug************/
			import mx.events.ItemClickEvent;
			import com.wisoft.interfaces.ILoadUserInfo;
			import com.wisoft.interfaces.ILoadSevicesURL;
			import com.wisoft.vo.Department;
			import com.wisoft.vo.Person;
			import com.wisoft.vo.UserInfo;
			import com.wisoft.framework.oms.OmsSelectPerson2;
			import com.wisoft.util.CommonFunctions;
			import mx.events.ModuleEvent;
			import com.wisoft.events.WindowCloseEvent;
			import com.wisoft.vo.SysConst; 
			import com.wisoft.framework.oms.CommonSelectDep;	
			
			private var moduleurl:String="";
			private var _ZONECODE:String='020000';
			private var _SERVICEURL:String='http://192.10.110.206:7001/wisoftintegrateframe/services';
			[Bindable]private var _personname:String = "未指定";
			//当前人员信息
			private var _USERINFO:UserInfo = new UserInfo();
			//常用人员信息列表
			[Bindable]private var personlist1:ArrayCollection = new ArrayCollection();
			private var shareData:SharedObject;
			private var departmentWithParent:Object;
			
			
			private function pinit():void{
				StyleManager.loadStyleDeclarations('css/style01/style01.swf');
				//设置安全沙箱
				Security.allowDomain("*");
			}
			private function init():void{
				//读取本地存储数据
				shareData=SharedObject.getLocal("myperson");
				_USERINFO = UserInfo.convert(shareData.data.curuser);
				if(shareData.data.userlist!=null)
					personlist1 =  shareData.data.userlist as ArrayCollection;
				this.dgofpro.dataProvider=this.personlist1;
				if(_USERINFO!=null&&_USERINFO.person!=null&&_USERINFO.ou!=null)
				{
					_personname=_USERINFO.ou.depname+"|"+_USERINFO.person.realname;
				}
				else
				CommonFunctions.showAlert("提示","请设置指定登录人员");
				moduleldr.url="zdm\\LoadZd.swf";
			}
			
    		private function showModule(event:mx.events.MenuEvent):void{
				if(event.item)
				{
					moduleurl=event.item.@moduleurl;
					if(moduleurl!="")
					{
						moduleldr.url=moduleurl;
					}
					else
					{
						moduleldr.url="";
					}
				}
			}
			
			private function readyModule( event:ModuleEvent ) : void
			{ 
			      var ml:ModuleLoader = event.target as ModuleLoader;
			      var ichild:ILoadSevicesURL = ml.child as ILoadSevicesURL;
			      var ichild2:ILoadUserInfo = ml.child as ILoadUserInfo;
			      if( ichild != null ) {
			               ichild._servicesurl =_SERVICEURL;
			      }
			      if(ichild2!=null)
			      {
				      	ichild2._userinfo = _USERINFO;
			      }
			}
			
			//---------------------------------------------------
			private function selectPersonByZoneid():void
			{
				var selectPage:OmsSelectPerson2=OmsSelectPerson2(PopUpManager.createPopUp(this, OmsSelectPerson2, true));
				selectPage.servicesurl=_SERVICEURL;
				selectPage.deptype=-1;
				selectPage.iscanuse=-1;
				selectPage.ismultiselect=true;
				selectPage.zonecode=this._ZONECODE;
				selectPage.addEventListener(WindowCloseEvent.WINDOW_CLOSE, selectWindowClose);
				CommonFunctions.centerTitleWindow(selectPage);
			}

			/*选人之后，保存*/
			private function selectWindowClose(event:WindowCloseEvent):void
			{
				/*返回选中的人员IS集合*/
				var resultList:ArrayCollection=event.ReturnValue as ArrayCollection;
				for	each(var a:Object in resultList )
				{
					ws_oms.findOmsInfoByPersonid.send(a.id);
				}
			}
			
			private function resultFindOmsInfoByPersonid(event:ResultEvent):void {
				if(event.result) {
					if(event.result.isSuccued) {
						var userinfo:UserInfo = new UserInfo;
						userinfo.authlist = event.result.authlist;
						var dt:Department = Department.convert(event.result.department);
						userinfo.department = dt;
						userinfo.deplist = event.result.deplist;
						userinfo.isadmin = event.result.isadmin;
						userinfo.isdepadmin = event.result.isdepadmin;
						userinfo.ismultdep = event.result.ismultdep;
						userinfo.iszoneadmin = event.result.iszoneadmin;
						userinfo.ou = Department.convert(event.result.ou);
						userinfo.person = Person.convert(event.result.person);
						userinfo.rolegrouplist = event.result.rolegrouplist;
						userinfo.teamlist = event.result.teamlist;
						
						//只添加列表中没有的
						if(personlist1.length>0)
						{	
							var ishave:Boolean=false;
							for each(var a:Object in personlist1 )
							{
								if(a.person.id == userinfo.person.id)
								{
									ishave=true;
									break;
								}
							}
							if(!ishave)
							{
								personlist1.addItem(userinfo);
							}
						}
						else
							personlist1.addItem(userinfo);
						shareData.data.userlist = personlist1;
						shareData.flush();
					}
				} else {
					CommonFunctions.showError("错误", event.result.message);
				}
			}
			
			private function unsel_person_done():void
			{
				var userinfo:UserInfo=UserInfo.convert(dgofpro.selectedItem) ;
				for (var a:int=0 ; a<personlist1.length;a++ )
				{
					if(personlist1[a].person.id == userinfo.person.id)
					{
						personlist1.removeItemAt(a);
						break;
					}
				}
				shareData.data.userlist = personlist1;
				shareData.flush();
			}
			
			private function sel_person_done():void
			{
				_USERINFO=dgofpro.selectedItem as UserInfo;
				
				if(_USERINFO==null)
				{
					CommonFunctions.showAlert("提示","没有人员被选中！");
					return;
				}
				moduleldr.url="com\\wisoft\\zdm\\LoadZd.swf";
				trace("用户信息加载成功");

				if(_USERINFO.ismultdep) {
					trace("用户包含多机构，选择登录机构");
					var selectdep:CommonSelectDep = CommonSelectDep(PopUpManager.createPopUp(this, CommonSelectDep, true));
					if(_USERINFO.deplist) {
						selectdep.deplist = _USERINFO.deplist;
						selectdep.person = _USERINFO.person;
					}
					selectdep.addEventListener(WindowCloseEvent.WINDOW_CLOSE, selectDepartmentWindowClosed);
					CommonFunctions.centerTitleWindow(selectdep);
				}
				//存储本地数据
				shareData.data.curuser =_USERINFO;
				shareData.flush();
				SysConst.curUser = _USERINFO;
				_personname = _USERINFO.ou.depname+"|"+_USERINFO.person.realname;
				proid.close();
			}
			private function selectDepartmentWindowClosed(event:WindowCloseEvent):void {
				if(event.IsConfirm) {
					if(event.RelatedObject) {
						_USERINFO.department = event.RelatedObject[0].department as Department;
						_USERINFO.ou = event.RelatedObject[1] as Department;
						_USERINFO.rolegrouplist = event.RelatedObject[2] as ArrayCollection;
						departmentWithParent = event.RelatedObject[0];
						trace("机构选择成功");
					}
				}
			}
    	]]>
    </mx:Script>  
    
	<mx:VBox left="0" right="0" bottom="0" top="0">
		<mx:HBox width="100%" height="70">
			<mx:VBox width="100%" height="100%" verticalGap="0">
				<mx:HBox width="100%" height="40" backgroundColor="#059BFB" horizontalAlign="left" verticalAlign="middle">
					<mx:Label text="Wisoft自动化测试平台 for RiaTest" fontSize="27" fontWeight="bold" color="#FFFFFF" fontStyle="italic"/>
				</mx:HBox>
				<mx:HBox width="100%" height="100%" horizontalAlign="left" verticalAlign="middle">
					<mx:MenuBar id="menu" dataProvider="{myXMLList}" labelField="@name"  height="100%" change="showModule(event)" fillAlphas="[1.0, 1.0]" fillColors="[#FFFFFF, #0018FF]"></mx:MenuBar>
					<mx:Spacer width="100%"/>
					<mx:PopUpButton id="proid" label="{_personname}" openAlways="true" width="160" textAlign="left" toolTip="{proid.label}">
						<mx:popUp>
							<mx:Canvas  width="160" height="200"> 
							<mx:VBox>
							<mx:DataGrid id="dgofpro" height="100%" textAlign="left" width="100%"
											   horizontalScrollPolicy="auto" borderStyle="none"   >
							<mx:columns> 
								<col:ObjectNestedColumn headerText="部门" dataField="ou.depname" width="80" />
								<col:ObjectNestedColumn headerText="姓名"  dataField="person.realname" width="80"  >
								</col:ObjectNestedColumn>
							</mx:columns> 
							</mx:DataGrid>
							<mx:ApplicationControlBar width="100%" >
								<mx:Button width="40" label="添加" click="selectPersonByZoneid()" ></mx:Button>
								<mx:Button width="40" label="删除" click="unsel_person_done()" ></mx:Button>
								<mx:Spacer width="100%"/>
								<mx:Button width="40" label="确定" click="sel_person_done()" ></mx:Button>
							</mx:ApplicationControlBar>
							</mx:VBox>	
						</mx:Canvas>
						</mx:popUp>
					</mx:PopUpButton>
				</mx:HBox>
			</mx:VBox>
		</mx:HBox>
		<mx:ModuleLoader width="100%" height="100%" id="moduleldr" ready="readyModule(event)">
		</mx:ModuleLoader>
	</mx:VBox>
	<mx:WebService id="ws_oms" wsdl="{_SERVICEURL}/OmsManager?wsdl" fault="CommonFunctions.faultmethod(event)">
		<mx:operation name="findOmsInfoByPersonid" result="resultFindOmsInfoByPersonid(event)" />
	</mx:WebService>
</mx:Application>
